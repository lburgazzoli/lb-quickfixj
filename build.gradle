buildscript {
    repositories {
        mavenCentral()
        jcenter()

        maven { url "http://repo.spring.io/snapshot" }
        maven { url "http://repo.spring.io/milestone" }
    }
    dependencies {
        classpath 'com.github.ben-manes:gradle-versions-plugin:0.5-beta-6'
        classpath 'org.gradle.api.plugins:gradle-nexus-plugin:0.7.1'
    }
}

// *****************************************************************************
//
// *****************************************************************************

subprojects {

    ext {
        versions = [
            slf4j                : '1.7.7',
            log4j                : '1.2.17',
            netty                : '4.0.21.Final',
            javassist            : '3.18.2-GA',
            mina1                : '1.1.7',
            osgi                 : '5.0.0',
            reactor              : '1.1.4.RELEASE',
            karaf                : '3.0.1',
            junit                : '4.11',
            disruptor            : '3.3.0',
            guava                : '18.0',
            openHftJavaLang      : '6.4.6',
            openHftJavaChronicle : '3.2.1',
            hazelcast            : '3.3',
            groovy               : '2.3.6',
            lbHazlecast          : '3.3.0.SNAPSHOT',
            lbKarafCommon        : '3.0.1.SNAPSHOT'
        ]
        
        gradleVersion               = '2.1'
        gradleScriptDir             = "${rootProject.projectDir}/gradle"
            
        if(project.hasProperty('sonatypeUsername') && project.hasProperty('sonatypePassword')) {
            nexusUsername = sonatypeUsername
            nexusPassword = sonatypePassword
        } else {
            nexusUsername = "$System.env.CI_DEPLOY_USERNAME"
            nexusUsername = "$System.env.CI_DEPLOY_PASSWORD"
        }
        
        prefix              = 'lb'
        isReleaseVersion    = !version.endsWith("SNAPSHOT")
        gitRoot             = "https://github.com/lburgazzoli"
        gitProject          = "https://github.com/lburgazzoli/lb-quickfixj"
        deploySnapshot      = "https://oss.sonatype.org/content/repositories/snapshots"
    }
        
    apply plugin: "java"
    apply plugin: "groovy"
    apply plugin: "maven"
    apply plugin: "osgi"
    apply plugin: 'nexus'
    apply plugin: 'com.github.ben-manes.versions'

    version             = '2.0.0-SNAPSHOT'
    group               = 'com.github.lburgazzoli'
    sourceCompatibility = '1.8'
    targetCompatibility = '1.8'

    repositories {
        mavenLocal()
        mavenCentral()

        maven { url "http://repo.springsource.org/release" }
        maven { url "http://repository.springsource.com/maven/bundles/release" }
        maven { url "http://repository.springsource.com/maven/bundles/external" }
        maven { url "http://oss.sonatype.org/content/repositories/snapshots" }
    }

    dependencies {
        compile "org.apache.commons:commons-math3:3.2"
        compile "org.apache.commons:commons-lang3:3.3.2"
        compile "org.apache.commons:commons-compress:1.5"
        compile "commons-io:commons-io:2.4"
        compile "commons-codec:commons-codec:1.9"
        compile "commons-collections:commons-collections:3.2.1"
        compile "commons-dbcp:commons-dbcp:1.4"
        compile "commons-dbutils:commons-dbutils:1.5"
        compile "commons-lang:commons-lang:2.6"
        compile "commons-logging:commons-logging:1.1.2"
        compile "commons-pool:commons-pool:1.6"
        compile "commons-net:commons-net:3.3"
        compile "commons-cli:commons-cli:1.2"
        compile "commons-configuration:commons-configuration:1.9"
        compile "log4j:log4j:$versions.log4j"
        compile "org.slf4j:slf4j-api:$versions.slf4j"
        compile "org.slf4j:slf4j-ext:$versions.slf4j"
        compile "org.slf4j:slf4j-log4j12:$versions.slf4j"
        compile "org.slf4j:jcl-over-slf4j:$versions.slf4j"
        compile "org.slf4j:jul-to-slf4j:$versions.slf4j"
        compile "com.google.guava:guava:$versions.guava"
        compile "com.lmax:disruptor:$versions.disruptor"
        compile "org.javolution:javolution-core-java:6.0.0"
        compile "org.dom4j:com.springsource.org.dom4j:1.6.1"
        compile "org.logicalcobwebs:com.springsource.org.logicalcobwebs.proxool:0.9.1"
        compile "net.openhft:lang:$versions.openHftJavaLang"
        compile "net.openhft:chronicle:$versions.openHftJavaChronicle"
        compile "org.codehaus.groovy:groovy:$versions.groovy"

        testCompile "junit:junit:$versions.junit"
    }

    jar {
        baseName = "${project.prefix}-${project.name}"

        manifest.attributes['Bundle-Description'     ] = "${project.prefix} :: ${project.name}"
        manifest.attributes['Bundle-Name'            ] = "${project.prefix} :: ${project.name}"
        manifest.attributes['Bundle-SymbolicName'    ] = "${project.group}-${project.prefix}-${project.name}"
        manifest.attributes['Bundle-Version'         ] = project.version
        manifest.attributes['Implementation-Title'   ] = "${project.group}-${project.prefix}-${project.name}-${project.version}"
        manifest.attributes['Implementation-Version' ] = project.version
        manifest.attributes['Implementation-Vendor'  ] = 'lb'

        manifest.classesDir = new File("$buildDir/classes/main")
        manifest.classpath  = project.runtimeClasspath
    }
    
    // *************************************************************************
    //
    // *************************************************************************

    nexus {
        attachJavadoc         = true
        attachSources         = true
        attachTests           = false
        sign                  = false
    }

    modifyPom {
        project {
            name          = "${project.prefix}-${project.name}"
            description   = "${project.prefix}-${project.name}"
            url           = "${project.gitProject}"
            packaging     = 'jar'

            licenses {
                license {
                    name         = "The Apache Software License, Version 2.0"
                    url          = "http://www.apache.org/licenses/LICENSE-2.0.txt"
                    distribution = "repo"
                }
            }

            scm {
                url                 = "${project.gitProject}"
                connection          = "scm:git:${project.gitProject}.git"
                developerConnection = "scm:git:${project.gitProject}.git"
            }
        }
    }
}

// *****************************************************************************
//
// *****************************************************************************

task wrapper(type: Wrapper) {
    gradleVersion = "$gradleVersion"
}

