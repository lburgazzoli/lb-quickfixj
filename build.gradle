// *****************************************************************************
// -Djava.util.logging.manager=org.apache.logging.log4j.jul.LogManager
// *****************************************************************************

buildscript {
    repositories {
        mavenCentral()
        jcenter()
    }
    dependencies {
        classpath 'com.github.ben-manes:gradle-versions-plugin:0.11.3'
        classpath 'com.bmuschko:gradle-nexus-plugin:2.3.1'
    }
}

// *****************************************************************************
//
// *****************************************************************************

allprojects {
    configurations {
        all {
            resolutionStrategy {
                componentSelection {
                    all { ComponentSelection selection ->
                        boolean rejected = [ 'M1', 'M2' ,'rc', 'Alpha' , 'Beta', "EA"].any { qualifier ->
                            return selection.candidate.version ==~ /(?i).*[.-]${qualifier}[.\d-]*/
                        }
                        if (rejected) {
                            selection.reject('Release candidate')
                        }
                    }
                }
            }
        }
    }
}

// *****************************************************************************
//
// *****************************************************************************

subprojects {
    ext {
        versions = [
            slf4j                : '1.7.12',
            log4j2               : '2.3',
            netty                : '4.0.29.Final',
            javassist            : '3.20.0-GA',
            mina1                : '1.1.7',
            osgi                 : '6.0.0',
            osgiCompendium       : '5.0.0',
            reactor              : '1.1.5.RELEASE',
            karaf                : '4.0.0',
            felixConfigAdmin     : '1.8.6',
            junit                : '4.12',
            disruptor            : '3.3.2',
            guava                : '18.0',
            openHftJavaLang      : '6.6.11',
            openHftChronicle     : '3.4.4',
            hazelcast            : '3.5',
            groovy               : '2.4.4',
            lbHazlecast          : '3.5.0-SNAPSHOT',
            lbKarafCommon        : '4.0.0-SNAPSHOT',
            commonsLang          : '3.4',
            commonsCollections   : '4.0',
            commonsPool          : '2.2',
            commonsIO            : '2.4',
            javolution           : '6.0.0',
            dom4j                : '1.6.1',
            proxool              : '0.9.1'
        ]
        
        gradleVersion               = '2.5'
        gradleScriptDir             = "${rootProject.projectDir}/gradle"
            
        if(project.hasProperty('sonatypeUsername') && project.hasProperty('sonatypePassword')) {
            nexusUsername = sonatypeUsername
            nexusPassword = sonatypePassword
        } else {
            nexusUsername = "$System.env.CI_DEPLOY_USERNAME"
            nexusPassword = "$System.env.CI_DEPLOY_PASSWORD"
        }
        
        prefix              = 'lb'
        isReleaseVersion    = !version.endsWith("SNAPSHOT")
        gitRoot             = "https://github.com/lburgazzoli"
        gitProject          = "https://github.com/lburgazzoli/lb-quickfixj"
        deploySnapshot      = "https://oss.sonatype.org/content/repositories/snapshots"
        osgiVersion         = project.version.replaceAll('-','.')
    }
        
    apply plugin: "java"
    apply plugin: "groovy"
    apply plugin: "maven"
    apply plugin: "osgi"
    apply plugin: 'com.bmuschko.nexus'
    apply plugin: 'com.github.ben-manes.versions'

    version             = '2.0.0-SNAPSHOT'
    group               = 'com.github.lburgazzoli'
    sourceCompatibility = '1.8'
    targetCompatibility = '1.8'

    repositories {
        mavenLocal()
        mavenCentral()

        maven { url "http://repo.springsource.org/release" }
        maven { url "http://repository.springsource.com/maven/bundles/release" }
        maven { url "http://repository.springsource.com/maven/bundles/external" }
        maven { url "http://oss.sonatype.org/content/repositories/snapshots" }
    }

    dependencies {
        compile("org.slf4j:slf4j-api:$versions.slf4j") { transitive = false }
        compile("org.slf4j:slf4j-ext:$versions.slf4j") { transitive = false }

        compile "org.apache.logging.log4j:log4j-api:$versions.log4j2"
        compile "org.apache.logging.log4j:log4j-core:$versions.log4j2"
        compile "org.apache.logging.log4j:log4j-jcl:$versions.log4j2"
        compile "org.apache.logging.log4j:log4j-jul:$versions.log4j2"
        compile "org.apache.logging.log4j:log4j-slf4j-impl:$versions.log4j2"

        compile "org.apache.commons:commons-lang3:$versions.commonsLang"
        compile "commons-io:commons-io:$versions.commonsIO"
        compile "org.apache.commons:commons-collections4:$versions.commonsCollections"
        
        compile "com.google.guava:guava:$versions.guava"
        compile "com.lmax:disruptor:$versions.disruptor"
        compile "org.javolution:javolution-core-java:$versions.javolution"
        compile "org.dom4j:com.springsource.org.dom4j:$versions.dom4j"
        compile "org.logicalcobwebs:com.springsource.org.logicalcobwebs.proxool:$versions.proxool"
        compile "net.openhft:lang:$versions.openHftJavaLang"
        compile "net.openhft:chronicle:$versions.openHftChronicle"
        compile "org.codehaus.groovy:groovy:$versions.groovy"

        testCompile "junit:junit:$versions.junit"
    }

    jar {
        baseName = "${project.prefix}-${project.name}"

        manifest.attributes['Bundle-Description'     ] = "${project.prefix} :: ${project.name}"
        manifest.attributes['Bundle-Name'            ] = "${project.prefix} :: ${project.name}"
        manifest.attributes['Bundle-SymbolicName'    ] = "${project.group}-${project.prefix}-${project.name}"
        manifest.attributes['Bundle-Version'         ] = osgiVersion
        manifest.attributes['Implementation-Title'   ] = "${project.group}-${project.prefix}-${project.name}-${osgiVersion}"
        manifest.attributes['Implementation-Version' ] = osgiVersion
        manifest.attributes['Implementation-Vendor'  ] = 'lb'
    }
    
    // *************************************************************************
    //
    // *************************************************************************

    nexus {
        sign    = false
    }

    extraArchive {
        sources = true
        tests   = false
        javadoc = false
    }

    modifyPom {
        project {
            name          = "${project.prefix}-${project.name}"
            description   = "${project.prefix}-${project.name}"
            url           = "${project.gitProject}"
            packaging     = 'jar'

            licenses {
                license {
                    name         = "The Apache Software License, Version 2.0"
                    url          = "http://www.apache.org/licenses/LICENSE-2.0.txt"
                    distribution = "repo"
                }
            }

            scm {
                url                 = "${project.gitProject}"
                connection          = "scm:git:${project.gitProject}.git"
                developerConnection = "scm:git:${project.gitProject}.git"
            }
        }
    }

    dependencyUpdates {
        revision = 'release'
    }
}

// *****************************************************************************
//
// *****************************************************************************

task wrapper(type: Wrapper) {
    gradleVersion = "$gradleVersion"
}

